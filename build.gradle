plugins {
    id("com.github.johnrengelman.shadow") version "8.1.1"
    id("io.micronaut.application") version "4.0.4"
    id("io.micronaut.aot") version "4.0.4"
    id("io.micronaut.test-resources") version "4.0.4"
    id("nu.studer.jooq") version "8.2.1"
    id("org.flywaydb.flyway") version "9.7.0"
}

version = "0.1"
group = "kz.aspansoftware"

repositories {
    mavenCentral()
}

dependencies {

    annotationProcessor("io.micronaut:micronaut-http-validation")
    annotationProcessor("io.micronaut.serde:micronaut-serde-processor")
    implementation("io.micronaut.serde:micronaut-serde-jackson")
    compileOnly("io.micronaut:micronaut-http-client")
    runtimeOnly("ch.qos.logback:logback-classic")
    testImplementation("io.micronaut:micronaut-http-client")

    implementation("io.micronaut.flyway:micronaut-flyway")
    implementation("io.micronaut.sql:micronaut-jdbc-hikari")
    implementation("io.micronaut.data:micronaut-data-jdbc")
    implementation("io.micronaut.sql:micronaut-jooq")
    implementation("net.coobird:thumbnailator:0.4.3")
    implementation("io.micronaut.rxjava3:micronaut-rxjava3")
    implementation("io.micronaut.views:micronaut-views-thymeleaf")

    implementation("io.micronaut.security:micronaut-security-jwt")
    implementation("io.micronaut.reactor:micronaut-reactor")

    runtimeOnly("org.postgresql:postgresql")
    runtimeOnly("org.yaml:snakeyaml")
    testImplementation("org.assertj:assertj-core")

    jooqGenerator 'org.postgresql:postgresql:42.2.4.jre7'

}


application {
    mainClass.set("kz.aspansoftware.Application")
}
java {
    sourceCompatibility = JavaVersion.toVersion("17")
    targetCompatibility = JavaVersion.toVersion("17")
}

graalvmNative.toolchainDetection = false
micronaut {
    runtime("netty")
    testRuntime("junit5")
    processing {
        incremental(true)
        annotations("kz.aspansoftware.*")
    }
    testResources {
        additionalModules.add("jdbc-postgresql")
    }
    aot {
        optimizeServiceLoading = false
        convertYamlToJava = false
        precomputeOperations = true
        cacheEnvironment = true
        optimizeClassLoading = true
        deduceEnvironment = true
        optimizeNetty = true
    }
}

configurations {
    flywayMigration
}

flyway {
    configurations = ['flywayMigration']
    url = 'jdbc:postgresql://localhost:5433/santec_shop'
    user = 'postgres'
    password = 'postgres'
}

jooq {
    configurations {
        main {
            generationTool {
                logging = org.jooq.meta.jaxb.Logging.WARN
                jdbc {
                    driver = 'org.postgresql.Driver'
                    url = flyway.url
                    user = flyway.user
                    password = flyway.password
                }
                generator {
                    name = 'org.jooq.codegen.DefaultGenerator'
                    database {
                        name = 'org.jooq.meta.postgres.PostgresDatabase'
                        inputSchema = 'public'
                        includes = '.*' //if tables begin with prefix then change to 'prefix.*'

                    }
                    generate {
                        deprecated = false
                        records = true
                        immutablePojos = true
                        fluentSetters = true
                    }
                    target {
                        packageName = 'kz.jooq.model'
                        directory = 'target/generated-sources/java'
                    }
                    strategy.name = 'org.jooq.codegen.DefaultGeneratorStrategy'
                }
            }
        }
    }
}

// configure jOOQ task such that it only executes when something has changed that potentially affects the generated JOOQ sources
// - the jOOQ configuration has changed (Jdbc, Generator, Strategy, etc.)
// - the classpath used to execute the jOOQ generation tool has changed (jOOQ library, database driver, strategy classes, etc.)
//// - the schema files from which the schema is generated and which is used by jOOQ to generate the sources have changed (scripts added, modified, etc.)
//tasks.named('generateJooq').configure {
//    // ensure database schema has been prepared by Flyway before generating the jOOQ sources
//    dependsOn tasks.named('flywayMigrate')
//
//    // declare Flyway migration scripts as inputs on the jOOQ task
//    inputs.files(fileTree('src/main/resources/db/migration'))
//            .withPropertyName('migrations')
//            .withPathSensitivity(PathSensitivity.RELATIVE)
//
//    // make jOOQ task participate in incremental builds (and build caching)
//    allInputsDeclared = true
//}